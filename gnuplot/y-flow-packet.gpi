#!/usr/bin/env gnuplot
reset session
set term pngcairo font 'Liberation Sans, 11' size 640, 480
set output 'gantt.png'

set colorsequence podo
set title 'Gannt Chart for Network Flows'
set offsets 0.5,0.5,0.5,0.5
set xtics 4
set grid x y
set key right bottom


maxPacketPerFlow = 8  # NOTE modify this value to display more packet

boxHeight         = 1.0 / maxPacketPerFlow
boxLeftOvershoot  = 1.0 / maxPacketPerFlow
boxRightOvershoot = 1.0 / maxPacketPerFlow * 1.5


# NOTE modify the following values only when needed


set style fill transparent solid 0.3 border lc black
set style arrow 1 lc var filled size screen 0.03,15,90
set style arrow 2 lc black empty size screen 0.03,15,90
set style arrow 3 lc black nohead
set style arrow 4 lc black nohead
set key noautotitle


flows = ''  # map flow-name to non-negative number
flow_add(d) = strstrt(flows, strcol(d)) ? '' : flows=flows.' '.strcol(d)
f(w) = words(substr(flows, 0, strstrt(flows, strcol(w))-1))

links = ''  # map link-name to non-negative number
link_add(d) = strstrt(links, strcol(d)) ? '' : links=links.' '.strcol(d)
l(w) = words(substr(links, 0, strstrt(links, strcol(w))-1))


plot 'data/flowspan1.data' \
    u (flow_add(1), link_add(4), 0):(f(1)):ytic(1) lc bgnd, \
 '' u 6:(f(1)):7:8:(f(1)-($3+1)*boxHeight):(f(1)-$3*boxHeight):(l(4)+1) with boxxyerror lc var, \
 '' u 6:(f(1)-($3+1)*boxHeight):(0):(boxLeftOvershoot):(l(4)+1) with vectors as 1, \
 '' u 6:(f(1)-($3+1)*boxHeight):(0):(boxLeftOvershoot) with vectors as 2, \
 '' u 6:(f(1)-($3+1)*boxHeight):($8-$6):(0) with vectors as 3, \
 '' u 9:(f(1)-($3+1)*boxHeight):(0):(boxRightOvershoot) with vectors as 4, \
 for [i=1:words(links)] NaN with boxes lc i title word(links, i)
