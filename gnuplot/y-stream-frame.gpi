#!/usr/bin/env gnuplot
reset session
set term qt noraise
# set term pngcairo enhanced
# set output 'gantt.png'

$Data << EOD
0       0       0       1       0       0       5   5   1
0       1       0       1       0       2       13  13  2
0       2       0       1       0       10      15  15  3
0       3       0       1       30      30      35  35  1
1       0       0       2       0       15      24  24  1
1       1       0       2       5       24      30  38  1
1       1       0       2       5       35      38  38  1
1       2       0       2       10      38      47  47  1
1       3       0       2       30      47      56  56  1
EOD

boxHalfHeight     = 0.2
boxLeftOvershoot  = 0.1
boxRightOvershoot = 0.08

set offsets 0,0,0.5-boxHalfHeight,0.5
set xrange [-1:]
set xtics 5
set grid x y
unset key


set style fill transparent solid 0.5 border lc black
set style arrow 1 lc var filled size screen 0.03,15,90
set style arrow 2 lc black empty size screen 0.03,15,90
set style arrow 3 lc black nohead
set style arrow 4 lc black nohead

set colorsequence podo

plot $Data \
       u 5:($1-boxHalfHeight):(0):(2*boxHalfHeight+$9*boxLeftOvershoot):2 with vectors as 1, \
    '' u 5:($1-boxHalfHeight):(0):(2*boxHalfHeight+$9*boxLeftOvershoot) with vectors as 2, \
    '' u 5:($1-boxHalfHeight):($8-$5):(0) with vectors as 3, \
    '' u 8:($1+boxHalfHeight):(0):(boxRightOvershoot) with vectors as 4, \
    '' u 5:1:6:7:($1-boxHalfHeight):($1+boxHalfHeight):2:ytic(1) with boxxyerror lc var
