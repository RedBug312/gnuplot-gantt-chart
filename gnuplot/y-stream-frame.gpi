#!/usr/bin/env gnuplot
reset session
# set term qt noraise font 'Liberation Sans, 10'
set term pngcairo font 'Liberation Sans, 10'
set output 'gantt.png'

set colorsequence podo
set title 'Gannt Chart for Schedule'
set offsets 0.5,0.5,0.5,0.5
set xtics 4
set grid x y
set key right bottom


maxFramePerStream = 8  # NOTE modify this value to display more frame

boxHeight         = 1.0 / maxFramePerStream
boxLeftOvershoot  = 1.0 / maxFramePerStream
boxRightOvershoot = 1.0 / maxFramePerStream * 1.5


# NOTE modify the following values only when needed


set style fill transparent solid 0.5 border lc black
set style arrow 1 lc var filled size screen 0.03,15,90
set style arrow 2 lc black empty size screen 0.03,15,90
set style arrow 3 lc black nohead
set style arrow 4 lc black nohead
set key noautotitle


streams = ''
stream_add(d) = strstrt(streams, strcol(d)) ? '' : streams=streams.' '.strcol(d)
s(w) = words(substr(streams, 0, strstrt(streams, strcol(w))-1))

links = ''
link_add(d) = strstrt(links, strcol(d)) ? '' : links=links.' '.strcol(d)
L(w) = words(substr(links, 0, strstrt(links, strcol(w))-1))


plot 'data/flowspan1.data' \
    u (stream_add(1), link_add(4), 0):(s(1)):ytic(1) lc bgnd, \
 '' u 6:(s(1)):7:8:(s(1)-($3+1)*boxHeight):(s(1)-$3*boxHeight):(L(4)+1) with boxxyerror lc var, \
 '' u 6:(s(1)-($3+1)*boxHeight):(0):(boxLeftOvershoot):(L(4)+1) with vectors as 1, \
 '' u 6:(s(1)-($3+1)*boxHeight):(0):(boxLeftOvershoot) with vectors as 2, \
 '' u 6:(s(1)-($3+1)*boxHeight):($8-$6):(0) with vectors as 3, \
 '' u 9:(s(1)-($3+1)*boxHeight):(0):(boxRightOvershoot) with vectors as 4, \
 for [i=1:words(links)] NaN with boxes lc i title word(links, i)

# plot 0 to add stream names and show them on ytics
# plot 1 for windows (boxes)
# plot 2 for arrival up-arrows
# plot 3 for arrival up-arrows again, but only border this time
# plot 4 for bottom lines, connecting from up-arrows to window-start
# plot 5 for little primes which denoting the finish of a stream frame
# plot 6 to show link names on key
